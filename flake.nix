# This flake was initially generated by fh, the CLI for FlakeHub (version 0.1.17)
{
  # A helpful description of your flake
  description = "Flake for Baton connector for Red Hat速 OpenShift速";

  # Flake inputs
  inputs = {
    baton.url = "github:conductorOne/baton";
    pre-commit-hooks.url = "github:cachix/git-hooks.nix";
    flake-schemas.url = "https://flakehub.com/f/DeterminateSystems/flake-schemas/*";

    nixpkgs.url = "https://flakehub.com/f/NixOS/nixpkgs/0.1.*";
  };

  # Flake outputs that other flakes can use
  outputs =
    {
      self,
      flake-schemas,
      nixpkgs,
      pre-commit-hooks,
      baton,
    }:
    let
      # Helpers for producing system-specific outputs
      supportedSystems = [
        "x86_64-linux"
        "aarch64-darwin"
        "x86_64-darwin"
        "aarch64-linux"
      ];
      forEachSupportedSystem =
        f:
        nixpkgs.lib.genAttrs supportedSystems (
          system:
          f {
            pkgs = import nixpkgs { inherit system; };
          }
        );

      ignoreFolders = [ "vendor" ];
    in
    {
      # Schemas tell Nix about the structure of your flake's outputs
      schemas = flake-schemas.schemas;

      # Development environments
      devShells = forEachSupportedSystem (
        { pkgs }:
        {
          default = pkgs.mkShell {
            buildInputs = self.checks.${pkgs.system}.pre-commit-check.enabledPackages;
            inherit (self.checks.${pkgs.system}.pre-commit-check) shellHook;
            # Pinned packages available in the environment
            packages =
              with pkgs;
              [
                golangci-lint
                jq
                openshift # provides the `oc` command

                baton.packages.${pkgs.system}.default
                # install the binary of this project
                self.packages.${pkgs.system}.default
              ] # also make available all packages used by pre-commit-check
              ++ self.checks.${pkgs.system}.pre-commit-check.enabledPackages;
          };
        }
      );

      packages = forEachSupportedSystem (
        { pkgs }:
        {
          default = pkgs.buildGoModule {
            pname = "baton-openshift";
            version = "0.0.1";

            src = ./.;

            modSha256 = null;
            vendorHash = null;

            meta = with pkgs.lib; {
              description = "Baton connector for Red Hat速 OpenShift速";
              license = licenses.asl20;
            };
          };
        }
      );

      checks = forEachSupportedSystem (
        { pkgs }:
        {
          pre-commit-check = pre-commit-hooks.lib.${pkgs.system}.run {
            src = ./.;
            hooks = {
              nil = {
                enable = true;
                excludes = ignoreFolders;
              };
              golangci-lint = {
                enable = true;
                excludes = ignoreFolders;
              };
              gotest = {
                enable = true;
                excludes = ignoreFolders;
              };
              yamllint = {
                enable = true;
                excludes = ignoreFolders;
              };
            };
          };
        }
      );
    };
}
